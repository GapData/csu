PREFIX skos:    <http://www.w3.org/2004/02/skos/core#>
PREFIX sctfl:   <http://plugins.linkedpipes.com/ontology/t-sparqlConstructToFileList#>
PREFIX void:    <http://rdfs.org/ns/void#>

CONSTRUCT {
  ?task a sctfl:Task ;
    sctfl:fileFormat "application/trig" ;
    sctfl:fileName ?fileName ;
    sctfl:hasTaskQuery ?taskQuery . 

  ?taskQuery a sctfl:TaskQuery ;
    sctfl:outputGraph ?dataset ;
    sctfl:query ?query .
}
WHERE {
  ?dataset a void:Linkset ;
    void:subjectsTarget ?subjectsTarget ;
    void:objectsTarget ?objectsTarget .

  BIND (concat(replace(str(?subjectsTarget), "^.*\\/(\\d+)$", "$1"),
               "-",
               replace(str(?objectsTarget), "^.*\\/(\\d+)$", "$1")) AS ?identifier)
  BIND (concat(?identifier, ".trig") AS ?fileName)

  BIND (replace("""
  PREFIX skos:    <http://www.w3.org/2004/02/skos/core#>
  PREFIX void:    <http://rdfs.org/ns/void#>

  CONSTRUCT {
    <%dataset%> ?linksetProperty ?linksetObject .
    ?s ?linkPredicate ?o .
  } WHERE {
    <%dataset%> ?linksetProperty ?linksetObject ; 
      void:subjectsTarget ?subjectsTarget ;
      void:objectsTarget ?objectsTarget ;
      void:linkPredicate ?linkPredicate .

    ?s ?linkPredicate ?o .
    FILTER (strstarts(str(?s), str(?subjectsTarget))
            &&
            strstarts(str(?o), str(?objectsTarget)))
  }
  """, "%dataset%", str(?dataset)) AS ?query)

  BIND ("http://localhost/" AS ?ns)
  BIND (iri(concat(?ns, "task/", ?identifier)) AS ?task)
  BIND (iri(concat(?ns, "task-query/", ?identifier)) AS ?taskQuery)
}
