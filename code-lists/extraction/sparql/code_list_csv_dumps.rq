PREFIX :         <http://example.com/>
PREFIX dcat:     <http://www.w3.org/ns/dcat#>
PREFIX dcterms:  <http://purl.org/dc/terms/>
PREFIX foaf:     <http://xmlns.com/foaf/0.1/>
PREFIX httpList: <http://plugins.linkedpipes.com/ontology/e-httpGetFiles#>

CONSTRUCT {
  :configuration a httpList:Configuration ;
    httpList:reference ?reference .

  ?reference a httpList:Reference ;
    httpList:fileUri ?csvUrl ;
    httpList:fileName ?fileName .
}
WHERE {
  {
    SELECT DISTINCT ?reference ?csvUrl ?fileName
    WHERE {
      GRAPH <https://nkod.opendata.cz/zdroj/datová-sada/NKOD> {
        [] a dcat:Dataset ;
          dcterms:publisher <https://linked.opendata.cz/zdroj/ekonomický-subjekt/00025593> ;
          foaf:page ?page .

        BIND ("http://apl.czso.cz/iSMS/" AS ?ns)
        BIND (str(?page) AS ?pageStr)
        FILTER strstarts(?pageStr, ?ns)
        BIND (replace(?pageStr, "^.*kodcis=(\\d+).*$", "$1") AS ?code)
        BIND (concat(?code, ".csv") AS ?fileName)
        BIND (iri(concat(str(:), ?code)) AS ?reference)
        BIND (concat("http://apl.czso.cz/iSMS/cisexp.jsp?format=2&separator=%2C&kodcis=", ?code) AS ?csvUrl)
      }
    }
  }
}
