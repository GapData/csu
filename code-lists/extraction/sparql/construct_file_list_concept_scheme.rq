PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX skos:    <http://www.w3.org/2004/02/skos/core#>
PREFIX sctfl:   <http://plugins.linkedpipes.com/ontology/t-sparqlConstructToFileList#>

CONSTRUCT {
  ?task a sctfl:Task ;
    sctfl:fileFormat "application/trig" ;
    sctfl:fileName ?fileName ;
    sctfl:hasTaskQuery ?taskQuery, ?taskConceptSchemeQuery, ?taskValidityQuery, ?taskAttributeQuery . 

  ?taskQuery a sctfl:TaskQuery ;
    sctfl:outputGraph ?dataset ;
    sctfl:query ?query .

  ?taskConceptSchemeQuery a sctfl:TaskQuery ;
    sctfl:outputGraph ?dataset ;
    sctfl:query ?conceptSchemeQuery .

  ?taskValidityQuery a sctfl:TaskQuery ;
    sctfl:outputGraph ?dataset ;
    sctfl:query ?validityQuery .

  ?taskAttributeQuery a sctfl:TaskQuery ;
    sctfl:outputGraph ?dataset ;
    sctfl:query ?attributeQuery . 
}
WHERE {
  ?dataset a skos:ConceptScheme ;
    dcterms:identifier ?identifier .
  BIND (concat(?identifier, ".trig") AS ?fileName)
  BIND (replace("""
  PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

  CONSTRUCT WHERE {
    ?concept skos:inScheme <%concept-scheme%> ;
      ?conceptProperty ?conceptObject .
  }
  """, "%concept-scheme%", str(?dataset)) AS ?query)

  BIND (replace("""
  PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
  PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

  CONSTRUCT WHERE {
    <%concept-scheme%> ?conceptSchemeProperty ?conceptSchemeObject .
    ?class rdfs:seeAlso <%concept-scheme%> ;
      ?classProperty ?classObject .
  }
  """, "%concept-scheme%", str(?dataset)) AS ?conceptSchemeQuery)

  BIND (replace("""
  PREFIX dcterms: <http://purl.org/dc/terms/>
  PREFIX skos:    <http://www.w3.org/2004/02/skos/core#>

  CONSTRUCT {
    ?validity ?validityProperty ?validityObject .
  } WHERE {
    {
      SELECT DISTINCT ?validity
      WHERE {
        <%concept-scheme%> ^skos:inScheme/dcterms:valid ?validity .
      }
    }
    ?validity ?validityProperty ?validityObject .
  }
  """, "%concept-scheme%", str(?dataset)) AS ?validityQuery)

  BIND (replace("""
  PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
  PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

  CONSTRUCT {
    ?statement a rdf:Statement ;
      rdf:subject ?concept ;
      rdf:predicate ?predicate ;
      rdf:object ?object .
  } WHERE {
    {
      SELECT DISTINCT ?concept
      WHERE {
         ?concept skos:inScheme <%concept-scheme%> .
      }
    }
    ?statement a rdf:Statement ;
      rdf:subject ?concept ;
      rdf:predicate ?predicate ;
      rdf:object ?object .
  }
  """, "%concept-scheme%", str(?dataset)) AS ?attributeQuery)

  BIND ("http://localhost/" AS ?ns)
  BIND (iri(concat(?ns, "task/", ?identifier)) AS ?task)
  BIND (iri(concat(?ns, "task-query/", ?identifier)) AS ?taskQuery)
  BIND (iri(concat(?ns, "task-query/", ?identifier, "/concept-scheme")) AS ?taskConceptSchemeQuery)
  BIND (iri(concat(?ns, "task-query/", ?identifier, "/validity")) AS ?taskValidityQuery)
  BIND (iri(concat(?ns, "task-query/", ?identifier, "/attributes")) AS ?taskAttributeQuery)
}
